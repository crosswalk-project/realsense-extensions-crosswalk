# Copyright (c) 2015 Intel Corporation. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//xwalk/common/xwalk_common.gni")

xwalk_idlgen("scene_perception_idl") {
  namespace = "realsense::jsapi"
  sources = [ "scene_perception.idl" ]
}

xwalk_js2c("scene_perception_js") {
  sources = [ "../js/scene_perception_api.js" ]
}

shared_library("scene_perception") {
  sources = [
    "scene_perception_extension.cc",
    "scene_perception_extension.h",
    "scene_perception_instance.cc",
    "scene_perception_instance.h",
    "scene_perception_object.cc",
    "scene_perception_object.h",
  ]
  deps = [
    "../../common:common_idl",
    "../../common:common_utils",
    ":scene_perception_idl",
    ":scene_perception_js",
    "//extensions/third_party/libpxc",
    "//xwalk/common:common_static",
  ]
  include_dirs = [
    "../../..",
    "$target_gen_dir",
  ]
}

action("make_dist") {
  script = "//extensions/realsense/common/dist.py"
  dll_file = "scene_perception.dll"
  sources = [
    "../../common/XWalkExtensionHooks_template.js"
  ]
  dist_dir = "$root_build_dir/realsense_extensions/scene_perception"
  outputs = [
    "$dist_dir/$dll_file",
    "$dist_dir/XWalkExtensionHooks.js",
  ]
  args = [
    "--extension-dll", rebase_path("$root_build_dir/$dll_file"),
    "--target-dir", rebase_path("$dist_dir"),
  ]
  deps = [ ":scene_perception" ]
}
